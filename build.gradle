plugins {
	id 'fabric-loom' version '1.13+'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    maven {
        url = "https://maven.fabricmc.net/"
    }
    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        url = "https://maven.shedaniel.me/"
    }
}

sourceCompatibility = 21
targetCompatibility = 21

ext.Versions = new Properties()
def versionFile = file("Versionfiles/mcversion-1.21.11.properties")
if (project.hasProperty('mcVersion')) {
    versionFile = file("Versionfiles/mcversion-${mcVersion}.properties")
}
Versions.load(versionFile.newReader())

archivesBaseName = "advancementinfo"
ext.projectVersion = "1.3.2"

version = "${project.projectVersion}-${Versions['minecraft_version']}"

loom {
    accessWidenerPath = file("src/main/resources/advancementinfo.accesswidener")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", Versions['minecraft_version']
    inputs.property "loader_version", Versions['loader_version']
    inputs.property "fabric_version", Versions['fabric_version']
    filesMatching("fabric.mod.json") {
        expand (
                "version": project.version,
                "minecraft_version": Versions['minecraft_version'],
                "loader_version": Versions['loader_version'],
                "fabric_version": Versions['fabric_version']
        )
    }
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"

    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"

    modCompileOnly "me.shedaniel.cloth:cloth-config-fabric:21.11.153"
    include "me.shedaniel.cloth:cloth-config-fabric:21.11.153"

    modCompileOnly "com.terraformersmc:modmenu:17.0.0-beta.1"
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
    from("CREDITS.txt")
    from("README.md")
}

modrinth {
    projectId = 'betteradvancementinfo'
    versionName = project.archivesBaseName
    uploadFile = remapJar
}


